buildscript {
    repositories {
        mavenLocal()
        mavenCentral()
        maven {
            name = "sonatype"
            url = "https://oss.sonatype.org/content/repositories/snapshots/"
        }
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
        maven {
            name = 'sponge'
            url = 'http://repo.spongepowered.org/maven'
        }
        jcenter()
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.2-SNAPSHOT'
        classpath 'commons-io:commons-io:2.5'
        classpath 'com.googlecode.java-diff-utils:diffutils:1.3.0'
        classpath 'pokechu22.test.begradle:begradle:0.1.0@jar'
        classpath 'org.spongepowered:mixingradle:0.5-SNAPSHOT'
    }
}

apply plugin: 'java'

dependencies {
    testCompile 'junit:junit:4.12'
    testCompile 'org.hamcrest:hamcrest-library:1.3'
    testCompile 'org.mockito:mockito-core:2.8.47'
}

version = "4.0.0.8-SNAPSHOT"
group = "wdl"
archivesBaseName = "LiteModWDL"
libsDirName = "../../libs/${version}"

apply plugin: 'net.minecraftforge.gradle.liteloader'
apply plugin: 'pokechu22.test.customsrg'
apply plugin: 'org.spongepowered.mixin'

minecraft {
    version = "1.10"
    mappings = "snapshot_20170801"
    runDir = "run"
}

extraSrgs {
    add file('../srgs/EntityHorseToEquineEntity.srg')
    add file('../srgs/AnimalChestToContainerHorseChest.srg')
}

sourceSets {
    main {
        java {
            srcDir project(':1.10').file("src/main/java")
            srcDir project(':share').file("src/main/java")
        }
        resources {
            srcDir "src/main/resources"
            srcDir project(':share').file("src/main/resources")
        }
    }
    mixin {
        java {
            srcDir project(':1.10.2-litemod').file("src/java6/java") // Re-use 1.10.2
            compileClasspath += sourceSets.main.output
            compileClasspath += sourceSets.main.compileClasspath
            runtimeClasspath += sourceSets.main.output
            runtimeClasspath += sourceSets.main.runtimeClasspath
        }
        // Refmap declaration must match the refmap name specified in the json config
        refMap = "mixins.LiteModWDL.refmap.json"
    }
    test {
        java {
            srcDir project(':1.10').file("src/test/java")
            srcDir project(':share').file("src/test/java")
        }
    }
    api {
        java {
            srcDir project(':share').file("src/api/java")
        }
    }
}

// We want Java 8 where possible
tasks.withType(JavaCompile) { javaTask ->
    javaTask.sourceCompatibility = "1.8"
    javaTask.targetCompatibility = "1.8"
}

sourceCompatibility = "1.8"
targetCompatibility = "1.8"

// Mixins need to be in Java 6 for this version of MC
compileMixinJava {
    sourceCompatibility = "1.6"
    targetCompatibility = "1.6"
}

mixin {
    defaultObfuscationEnv notch
}

litemod {
    json {
        name = "LiteModWDL"
        displayName = "World Downloader for Liteloader"
        author = "uyjulian, pokechu22"
        mcversion = "1.10"
        mixinConfigs += "mixins.LiteModWDL.json"
        version = project.version
    }
}

// Use tabs instead of spaces
fixMcSources {
    astyleConfig = "../astyle.cfg"
    // TEMPORARY, until I can figure out an automatic patch system.
    doFirst {
        def field = net.minecraftforge.gradle.tasks.PostDecompileTask.class.getDeclaredField('patchesMap')
        field.accessible = true
        field.get(fixMcSources).put('net.minecraft.entity.passive.EquineEntity.java', file('../srgs/EE_1.10.x.patch'))
    }
}

// Ensure that the API code, Java 6 mixin code, and litemod JSON are in the jar
jar {
    from sourceSets.mixin.output
    from sourceSets.api.output
    from litemod.outputs
}

// We don't currently want a source jar (doesn't work well for base edits, and the version info doesn't work yet)
sourceJar.enabled = false

// TEMPORARY, until this is moved to begradle
processResources {
    rename { name ->
        if (name.endsWith(".lang")) {
            def index = name.indexOf("_")
            return name.substring(0, index) + name.substring(index, index + 3).toUpperCase() + ".lang"
        }
    }
}
